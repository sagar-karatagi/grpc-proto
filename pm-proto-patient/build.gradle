plugins {
    id "java"
    id "com.google.protobuf" version "0.9.4"
    id "maven-publish"
}
//group = 'com.pm'
//version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:1.69.0"
    implementation "io.grpc:grpc-protobuf:1.69.0"
    implementation "io.grpc:grpc-stub:1.69.0"
    implementation 'com.google.protobuf:protobuf-java:3.25.3'
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    // compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // optional if needed
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.3"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.69.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
            task.plugins {
                grpc {}
            }
        }
    }
}
/*
We don't need to add the superset because we have the proto in default location
 */

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = "pm-proto-patient"
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sagar-karatagi/grpc-protos")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
